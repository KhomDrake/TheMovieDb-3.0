plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id "jacoco"
}

apply from: "$rootDir/gradle/coverage.gradle"
apply from: "$rootDir/gradle/config.gradle"

android {
    namespace 'com.vlv.themoviedb'
    compileSdk config.compileSdk

    defaultConfig {
        applicationId "com.vlv.themoviedb"

        minSdk config.minSdk
        targetSdkVersion config.targetSdkVersion

        versionCode 2
        versionName "0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        upload {
            storeFile file("$rootDir/distribution/key_store")
            storePassword "asdkljasldka"
            keyAlias "lkdajsldas"
            keyPassword "dljshadjahsk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
        }
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true
            minifyEnabled false
        }
    }

    flavorDimensions += "uitool"
    productFlavors {
        create("compose") {
            dimension = "uitool"
            applicationIdSuffix = ".compose"
            versionNameSuffix = "-compose"
        }
        create("views") {
            dimension = "uitool"
            applicationIdSuffix = ".views"
            versionNameSuffix = "-views"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
        exclude "**/attach_hotspot_windows.dll"
        exclude "META-INF/licenses/**"
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.1"
    }

    testOptions {
        packagingOptions {
            jniLibs {
                useLegacyPackaging = true
            }
        }
    }
}

dependencies {

    // General
    implementation project(':libraries:imperiya:core')
    implementation project(':libraries:bondsmith')
    implementation project(':data:database')
    implementation project(':data:network')
    implementation project(':data:local')
    implementation project(':data:common')
    implementation project(':tools:extensions')
    implementation project(':core:ui')
    implementation project(':feature:configuration:domain')
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.startup)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.compose.material3)

    // Compose
    composeImplementation(libs.androidx.compose.activity)
    composeImplementation(libs.androidx.compose.livedata)
    composeImplementation(libs.androidx.compose.tooling.preview)
    composeImplementation(libs.koin.compose)
    composeImplementation(libs.compose.shimmer)
    composeImplementation project(':feature:movie:presentation')
    composeImplementation project(':feature:series:presentation')
    composeImplementation project(':feature:genre:presentation')
    composeImplementation project(':feature:favorite:presentation')
    composeImplementation project(':feature:movie:data')
    composeImplementation project(':feature:series:data')
//    composeDebugImplementation(libs.androidx.compose.tooling.debug)

    // Views
    viewsImplementation project(':feature:movie:presentation')
    viewsImplementation project(':feature:series:presentation')
    viewsImplementation project(':feature:people:presentation')
    viewsImplementation project(':feature:search:presentation')
    viewsImplementation project(':feature:favorite:presentation')
    viewsImplementation project(':feature:configuration:presentation')
    viewsImplementation project(':feature:genre:presentation')

    viewsImplementation project(':feature:configuration:domain')

    viewsImplementation project(':feature:movie:data')
    viewsImplementation project(':feature:series:data')

    viewsImplementation(libs.google.material)
    viewsImplementation(libs.shimmer)
    viewsImplementation(libs.pagerindicator)
    viewsImplementation(libs.tests.androidx.fragment.testing)
    viewsImplementation(libs.tests.androidx.test.core)

    // Debug
    debugImplementation(libs.bigbrother.core)
    debugImplementation(libs.bigbrother.network)
    debugImplementation(libs.bigbrother.log)

    // Tests
    testImplementation project(':tools:test')
    androidTestImplementation project(':tools:test')
    androidTestImplementation project(':feature:favorite:domain')
}
plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id "jacoco"
}

apply from: "$rootDir/gradle/coverage.gradle"
apply from: "$rootDir/gradle/config.gradle"

android {
    namespace 'com.vlv.themoviedb'
    compileSdk config.compileSdk

    defaultConfig {
        applicationId "com.vlv.themoviedb"

        minSdk config.minSdk
        targetSdkVersion config.targetSdkVersion

        versionCode 1
        versionName "0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        upload {
            storeFile file("$rootDir/distribution/key_store")
            storePassword "asdkljasldka"
            keyAlias "lkdajsldas"
            keyPassword "dljshadjahsk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
        }
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true
            minifyEnabled false
        }
    }

    flavorDimensions += "uitool"
    productFlavors {
        create("compose") {
            dimension = "uitool"
            applicationIdSuffix = ".compose"
            versionNameSuffix = "-compose"
        }
        create("views") {
            dimension = "uitool"
            applicationIdSuffix = ".views"
            versionNameSuffix = "-views"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
        exclude "**/attach_hotspot_windows.dll"
        exclude "META-INF/licenses/**"
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }
}

dependencies {

    implementation project(':imperiya')
    implementation project(':network')
    implementation project(':tools:bondsmith')
    implementation project(':tools:extensions')
    implementation project(':feature:movie')
    implementation project(':feature:series')
    implementation project(':feature:people')
    implementation project(':feature:search')
    implementation project(':feature:favorite')
    implementation project(':feature:configuration')
    implementation project(':feature:genre')
    implementation project(':common')

    debugImplementation(libs.bigbrother.core)
    debugImplementation(libs.bigbrother.network)
    debugImplementation(libs.bigbrother.log)

    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.startup)
    implementation(libs.google.material)

    viewsImplementation(libs.shimmer)
    viewsImplementation(libs.pagerindicator)

    testImplementation project(':tools:test')
    androidTestImplementation project(':tools:test')
}